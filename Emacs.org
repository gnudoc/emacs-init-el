#+title: Emacs From Scratch config
#+PROPERTY: header-args:emacs-lisp :tangle ./init.el :mkdirp yes

* Preamble
#+begin_src emacs-lisp
  ;; Note that init.el is generated from ./Emacs.org - that is the file that should be edited
  ;; on first install, do M-x all-the-icons-install-fonts, M-x treesit-install-language-grammar
  ;; and M-x pdf-tools-install
#+end_src

* Package System Setup (elpaca)
** Elpaca
Using [[https://github.com/progfolio/elpaca][elpaca]] for fast, parallel, and reproducible package management.
This code is based on the official README.

#+begin_src emacs-lisp
  ;; --- Elpaca Bootstrap ---
  (defvar elpaca-installer-version 0.11)
  (defvar elpaca-directory (expand-file-name "elpaca/" user-emacs-directory))
  (defvar elpaca-builds-directory (expand-file-name "builds/" elpaca-directory))
  (defvar elpaca-repos-directory (expand-file-name "repos/" elpaca-directory))
  (defvar elpaca-order '(elpaca :repo "https://github.com/progfolio/elpaca.git"
                               :ref nil :depth 1 :inherit ignore
                               :files (:defaults "elpaca-test.el" (:exclude "extensions"))
                               :build (:not elpaca--activate-package)))
  (let* ((repo  (expand-file-name "elpaca/" elpaca-repos-directory))
         (build (expand-file-name "elpaca/" elpaca-builds-directory))
         (order (cdr elpaca-order))
         (default-directory repo))
    (add-to-list 'load-path (if (file-exists-p build) build repo))
    (unless (file-exists-p repo)
      (make-directory repo t)
      (when (<= emacs-major-version 28) (require 'subr-x))
      (condition-case-unless-debug err
          (if-let* ((buffer (pop-to-buffer-same-window "*elpaca-bootstrap*"))
                    ((zerop (apply #'call-process `("git" nil ,buffer t "clone"
                                                        ,@(when-let* ((depth (plist-get order :depth)))
                                                            (list (format "--depth=%d" depth) "--no-single-branch"))
                                                        ,(plist-get order :repo) ,repo))))
                    ((zerop (call-process "git" nil buffer t "checkout"
                                          (or (plist-get order :ref) "--"))))
                    (emacs (concat invocation-directory invocation-name))
                    ((zerop (call-process emacs nil buffer nil "-Q" "-L" "." "--batch"
                                          "--eval" "(byte-recompile-directory \".\" 0 'force)")))
                    ((require 'elpaca))
                    ((elpaca-generate-autoloads "elpaca" repo)))
              (progn (message "%s" (buffer-string)) (kill-buffer buffer))
            (error "%s" (with-current-buffer buffer (buffer-string))))
        ((error) (warn "%s" err) (delete-directory repo 'recursive))))
    (unless (require 'elpaca-autoloads nil t)
      (require 'elpaca)
      (elpaca-generate-autoloads "elpaca" repo)
      (let ((load-source-file-function nil)) (load "./elpaca-autoloads"))))
  (add-hook 'after-init-hook #'elpaca-process-queues)
  (elpaca `(,@elpaca-order))

  ;; --- Elpaca use-package Integration ---
  
  ;; Install use-package support
  (elpaca elpaca-use-package
    ;; Enable use-package :ensure support for elpaca.
    (elpaca-use-package-mode)
    ;; Make elpaca manage all use-package declarations by default.
    (setq elpaca-use-package-by-default t))

#+end_src
** Shell PATH
Ensure Emacs inherits the system $PATH so it can test for programs in the rest of the config
#+begin_src emacs-lisp
  (use-package exec-path-from-shell
    :ensure t
    :config
    (when (memq window-system '(x mac ns))
      (exec-path-from-shell-initialize)))
#+end_src
* Core Emacs Config
Using =use-package= to configure the built-in Emacs settings.
This replaces the old "Basic UI Config" and "Font config" sections.
On linux, we want [[https://github.com/tonsky/FiraCode][FiraCode]] and [[https://fonts.google.com/specimen/Cantarell][Cantarell]]; on windows, Consolas and Segoe UI are defaults apparently.
#+begin_src emacs-lisp
  (use-package emacs
     
    :ensure nil ;; This is a built-in package
    :init
    ;; Settings to apply very early, before config is fully loaded
    (setq inhibit-startup-message t)

    :config
    ;; --- Basic UI Tweaks ---
    (column-number-mode)
    (global-display-line-numbers-mode t)
    (setq visible-bell t)

    ;; Disable line numbers in modes where they don't make sense
    (dolist (mode '(org-mode-hook
                    term-mode-hook
                    vterm-mode-hook
                    shell-mode-hook
                    eshell-mode-hook
                    eww-mode-hook
                    pdf-view-mode-hook)) 
      (add-hook mode (lambda () (display-line-numbers-mode 0)))) 

    ;; --- GUI Specific Tweaks ---
    (when (display-graphic-p) 
      (add-to-list 'default-frame-alist '(alpha-background . 95))
      (add-to-list 'default-frame-alist '(fullscreen . maximized)) 
      (scroll-bar-mode -1) 
      (tool-bar-mode -1) 
      (tooltip-mode -1) 
      (menu-bar-mode -1) 
      (set-fringe-mode 5)) 

    ;; --- Font Configuration ---
    (cond
     ((find-font (font-spec :name "FiraCode Nerd Font"))
      (message "Using FiraCode Nerd Font")
      (set-face-attribute 'default nil :font "FiraCode Nerd Font" :height 130) 
      (set-face-attribute 'fixed-pitch nil :font "FiraCode Nerd Font" :height 130)) 
     ((find-font (font-spec :name "Consolas"))
      (message "Using Consolas as a fallback")
      (set-face-attribute 'default nil :font "Consolas" :height 130) 
      (set-face-attribute 'fixed-pitch nil :font "Consolas" :height 130))
     (t
      (message "Neither FiraCode nor Consolas found, using emacs default.")))

    (cond
     ((find-font (font-spec :name "Cantarell"))
      (message "Using Cantarell for variable-pitch") 
      (set-face-attribute 'variable-pitch nil :font "Cantarell" :height 140 :weight 'regular)) 
     ((find-font (font-spec :name "Segoe UI"))
      (message "Using Segoe UI as a fallback for variable pitch") 
      (set-face-attribute 'variable-pitch nil :font "Segoe UI" :height 140 :weight 'regular)) 
     (t
      (message "Neither Cantarell nor Segoe UI found, using emacs default.")))) 
#+end_src

* More UI and UX Config
Theming, Completion and Help
** Colour Theme
Take suggestions from [[https://github.com/hlissner/emacs-doom-themes][doom-themes]]
#+begin_src emacs-lisp
  (use-package doom-themes
    :ensure t
    :init (load-theme 'doom-dracula t))
#+end_src
** Modeline
Another element of doom emacs that's nice is [[https://github.com/seagle0128/doom-modeline][modeline]]. It has a bunch of its own config options, and on first run it needs =M-x all-the-icons-install-fonts=
#+begin_src emacs-lisp
  (use-package all-the-icons :ensure t)
  (use-package nerd-icons :ensure t)
  (use-package doom-modeline
    :ensure t
    :after (all-the-icons nerd-icons)
    :config
    (require 'all-the-icons) ;;not sure if this is needed
    (if (and (display-graphic-p) (member "all-the-icons" (font-family-list)))
  	(progn
  	  (doom-modeline-mode 1)
  	  (setq doom-modeline-height 15))
          (message "doom-modeline not enabled - either fonts not found or we're in a TTY. M-x all-the-icons-install-fonts")))
#+end_src
** Which Key
[[https://github.com/justbur/emacs-which-key][which-key]] offers all completions for any key prefix - now part of default emacs.
#+begin_src emacs-lisp
  (use-package which-key
    :ensure t
    :init (which-key-mode)
    :diminish which-key-mode
    :config (setq which-key-idle-delay 0.5))
#+end_src
** Vertico, Consult, Marginalia
The modern, minimal replacement for Ivy, Counsel, and Ivy-rich.
   =vertico= is the vertical completion UI.
   =orderless= is the completion style (replaces =ivy=).
   =consult= provides the powerful search commands (replaces =counsel=).
   =marginalia= adds the rich annotations (replaces =ivy-rich=).

#+begin_src emacs-lisp
  (use-package vertico
    :ensure t
    :init
    (vertico-mode 1)
    ;; Add cycling with TAB
    (setq vertico-cycle t))

  (use-package orderless
    :ensure t
    :config
    (setq completion-styles '(orderless)
          completion-category-defaults nil
          completion-category-overrides '((file (styles . (partial-completion))))))

  (use-package consult
    :ensure t
    :bind
    ;; Re-bind the keys we had for counsel
    (("M-x" . execute-extended-command)
     ("C-x b" . consult-buffer) ;; A good replacement for counsel-ibuffer
     ;; C-x C-f (find-file) uses vertico automatically
     :map minibuffer-local-map
     ("C-r" . consult-history)))

  (use-package marginalia
    :ensure t
    :after vertico
    :init
    (marginalia-mode 1))
#+end_src
** Helpful
[[https://github.com/Wilfred/helpful][Helpful]] adds more info to the =describe-*= buffers.
We're changing this from counsel to corfu.
#+begin_src emacs-lisp
  (use-package helpful
    :ensure t
    :bind
    ;; Remap the default describe-* commands to helpful's versions
    ([remap describe-function] . helpful-function)
    ([remap describe-command] . helpful-command)
    ([remap describe-variable] . helpful-variable)
    ([remap describe-key] . helpful-key))
#+end_src

* Org Mode
The famous magic [[https://orgmode.org][Org Mode]]. All sorts of functions. Rich doc editing, project planning, task/time tracking, literate coding, blogging engine, and more.
** Fonts in Org
A helper function for fonts, with a conditional checking for the variable fonts we like
#+begin_src emacs-lisp
  (defun nij/org-font-setup ()
    (font-lock-add-keywords 'org-mode
  			  '(("^ *\([-]\) "
  			     (0 (prog1 () (compose-region (match-beginning 1) (match-end 1) "•"))))))
    (let ((variable-font
           (cond
            ((find-font (font-spec :name "Cantarell")) "Cantarell")
            ((find-font (font-spec :name "Segoe UI")) "Segoe UI")
            (t nil))))
      (when variable-font
        (dolist (face '((org-level-1 . 1.2)
                        (org-level-2 . 1.1)
                        (org-level-3 . 1.05)
                        (org-level-4 . 1.0)
                        (org-level-5 . 1.0)
                        (org-level-6 . 1.0)
                        (org-level-7 . 1.0)
                        (org-level-8 . 1.0)))
          (set-face-attribute (car face) nil :font variable-font :weight 'regular :height (cdr face)))))

    (set-face-attribute 'org-block nil :foreground nil :inherit 'fixed-pitch)
    (set-face-attribute 'org-code nil :inherit '(shadow fixed-pitch))
    (set-face-attribute 'org-table nil :inherit '(shadow fixed-pitch))
    (set-face-attribute 'org-verbatim nil :inherit '(shadow fixed-pitch))
    (set-face-attribute 'org-special-keyword nil :inherit '(font-lock-comment-face fixed-pitch))
    (set-face-attribute 'org-meta-line nil :inherit '(font-lock-comment-face fixed-pitch))
    (set-face-attribute 'org-checkbox nil :inherit 'fixed-pitch))

#+end_src
** Basic Config
Here are some basics. See systemcrafters emacs-from-scratch for examples.
#+begin_src emacs-lisp
  (defun nij/org-mode-setup ()
    (org-indent-mode)
    (variable-pitch-mode 1)
    (visual-line-mode 1)
    )

  (use-package org
     
    :ensure nil
    :hook (org-mode . nij/org-mode-setup)
    :config
    (setq org-ellipsis " ▾")
    (nij/org-font-setup))
#+end_src
** Org Superstar
Use org-superstar for enhanced display, including custom priorities.
#+begin_src emacs-lisp
  (use-package org-superstar
    :ensure t
    :hook (org-mode . org-superstar-mode)
    :custom
    (org-superstar-prettify-item-priority t)
    (setq org-superstar-prettifiers
  	(list
  	 '(org-superstar-priority-prettifier . org-superstar-prettify-priority)
  	 ))
    (setq org-superstar-priority-faces nil)
    (org-superstar-headline-bullets-list '("◉" "○" "●" "○" "●" "○" "●"))
    )
#+end_src
** Configure Babel Languages
Add a list item to org-babel-load-languages for each language we want to do code blocks for. The conf-unix line lets us do key-value pairs (eg meaning=42) which a lot of *nix programs use for config.
More info [[https://orgmode.org/worg/org-contrib/babel/languages.html][here]].
#+begin_src emacs-lisp
  (let ((babel-languages '((emacs-lisp . t))))
    (when (executable-find "python")
      (push '(python . t) babel-languages))
    (when (executable-find "haskell")
      (push '(haskell . t) babel-languages))
    (org-babel-do-load-languages 'org-babel-load-languages babel-languages))
  (push '("conf-unix" . conf-unix) org-src-lang-modes)

#+end_src
** Structure Templates
This allows you to type <el then TAB or <py then TAB and so on to get code blocks inserted into org docs. You can even use it for the custom bits like :tangle or :mkdirp if needed.
More [[https://orgmode.org/manual/Structure-Templates.html][here]].
#+begin_src emacs-lisp
  (use-package org-tempo
     
    :ensure nil ; It's part of org so won't needed installed
    :after org  ; Ensure org is loaded first
    :config
    (add-to-list 'org-structure-template-alist '("sh" . "src shell"))
    (add-to-list 'org-structure-template-alist '("el" . "src emacs-lisp"))
    (add-to-list 'org-structure-template-alist '("hs" . "src haskell")))

#+end_src
** Auto-tangle Config Files
Any time an org-mode buffer gets saved, our function gets run. If the org-mode file being saved is this one, then the code snippets are all sent to their respective files.
#+begin_src emacs-lisp

  (defun nij/org-babel-tangle-config ()
    "Automatically tangle our Emacs.org config file when we save it"
    (when (string-equal (buffer-file-name)
  		      (expand-file-name "~/.emacs.d/Emacs.org"))
      ;; Dynamic scoping example
      (let ((org-confirm-babel-evaluate nil))
        (org-babel-tangle))))
  (add-hook 'org-mode-hook (lambda () (add-hook 'after-save-hook #'nij/org-babel-tangle-config)))

#+end_src
* Dev Stuff
** Languages
*** Tree-sitter Language Grammars
The command =M-x treesit-install-language-grammar= , which requires *C and git*, will install a language grammar for tree-sitter, but you have to point it at the grammar using the following variable. See [[https://www.masteringemacs.org/article/how-to-get-started-tree-sitter][Mastering Emacs]] for more.
#+begin_src emacs-lisp
  (setq treesit-language-source-alist
    '((bash "https://github.com/tree-sitter/tree-sitter-bash")
      (c "https://github.com/tree-sitter/tree-sitter-c")
      (cpp "https://github.com/tree-sitter/tree-sitter-cpp")
      (cmake "https://github.com/uyha/tree-sitter-cmake")
      (css "https://github.com/tree-sitter/tree-sitter-css")
      (elisp "https://github.com/Wilfred/tree-sitter-elisp")
      (go "https://github.com/tree-sitter/tree-sitter-go")
      (haskell "https://github.com/tree-sitter/tree-sitter-haskell")
      (html "https://github.com/tree-sitter/tree-sitter-html")
      (java "https://github.com/tree-sitter/tree-sitter-java")
      (javascript "https://github.com/tree-sitter/tree-sitter-javascript" "master" "src")
      (json "https://github.com/tree-sitter/tree-sitter-json")
      (make "https://github.com/alemuller/tree-sitter-make")
      (markdown "https://github.com/ikatyang/tree-sitter-markdown")
      (php "https://github.com/tree-sitter/tree-sitter-php" "master" "php/src")
      (python "https://github.com/tree-sitter/tree-sitter-python")
      (regex "https://github.com/tree-sitter/tree-sitter-regex")
      (rust "https://github.com/tree-sitter/tree-sitter-rust")
      (toml "https://github.com/tree-sitter/tree-sitter-toml")
      (tsx "https://github.com/tree-sitter/tree-sitter-typescript" "master" "tsx/src")
      (typescript "https://github.com/tree-sitter/tree-sitter-typescript" "master" "typescript/src")
      (yaml "https://github.com/ikatyang/tree-sitter-yaml")))
  (add-to-list 'major-mode-remap-alist '(c-mode . c-ts-mode))
  (add-to-list 'major-mode-remap-alist '(c++-mode . c++-ts-mode))
#+end_src
*** Language Server (Eglot)
This section configures the inbuilt (since version 29ish) LSP client. Eglot can find most lang servers (like =clangd=, =jdtls=, etc) in our PATH, but we need to provide explicit config for servers in non-standard locations.
#+begin_src emacs-lisp

  (defun shell-other-window ()
    "Open a 'shell' buffer in another window"
    (interactive)
    (let ((buf (shell)))
      (switch-to-buffer (other-buffer buf))
      (switch-to-buffer-other-window buf))
    )

  (use-package flymake :ensure t)
  (use-package jsonrpc :ensure t)
  (use-package eglot
    :ensure t
    :hook ((haskell-mode . eglot-ensure)
  	 (java-ts-mode . eglot-ensure)
  	 (python-mode . eglot-ensure)
  	 (js-mode . eglot-ensure)
  	 (typescript-mode . eglot-ensure)
  	 (c-ts-mode . eglot-ensure)
  	 (c++-ts-mode . eglot-ensure)
  	 )
    :bind (:map eglot-mode-map
  	      ("M-." . xref-find-definitions)
  	      ("M-," . xref-pop-marker-stack)
  	      ("C-c a" . eglot-code-actions)
  	      )
    :config
    (let ((haskell-lsp (expand-file-name "~/.ghcup/bin/haskell-language-server-wrapper")))
      (when (file-exists-p haskell-lsp)
        (add-to-list 'eglot-server-programs
                     `(haskell-mode . (,haskell-lsp "--lsp")))))
    (setq eglot-auto-server-display nil) ;Don't autodisplay server-buffer on restart
    (setq eglot-reconnect-on-change t)
    (setq eglot-autodocument t) ; on mouse-over
    (setq eglot-autodocument-delay 1)
    (setq eglot-display-buffer-function #'eglot-display-buffer-at-bottom)
    )
#+end_src
*** C++
C, C++ and cmake settings - using the newer treesitter modes
#+begin_src emacs-lisp
  (defun nij/cmake-configure-preset ()
    "Run 'cmake --preset default' in the project root."
    (interactive)
    (if (executable-find "cmake")
        (let ((compile-command "cmake --preset default")) (compile compile-command))
      (message "Error: 'cmake' not in PATH.")
      ))
#+end_src

#+begin_src emacs-lisp
  (defun nij/c-ts-mode-common-setup ()
    "Common setup for C/C++ tree-sitter modes."
    ;; This replaces 'c-basic-offset' and 'c-set-style'.
    (setq-local treesit-indent-level 4)
      
    ;; Bind keys to the C TS map. 
    ;; c++-ts-mode inherits from c-ts-mode, so it gets these keys too.
    (define-key c-ts-mode-map (kbd "C-c c") #'compile)
    (define-key c-ts-mode-map (kbd "C-c s") #'shell-other-window)
    )
  (defun nij/cpp-ts-mode-setup ()
    "C++ specific setup."
    ;; Bind your C++ specific key
    (define-key c++-ts-mode-map (kbd "C-c C-p") #'nij/cmake-configure-preset)
    )
  ;; Add the hooks for the new modes
  (add-hook 'c-ts-mode-hook #'nij/c-ts-mode-common-setup)
  (add-hook 'c++-ts-mode-hook #'nij/cpp-ts-mode-setup)
#+end_src
*** Java
Java settings - using the newer treesitter version
#+begin_src emacs-lisp

(add-to-list 'major-mode-remap-alist '(java-mode . java-ts-mode))

(defun nij/java-ts-mode-setup ()
  "Keybinds for java-ts-mode."
  (define-key java-ts-mode-map (kbd "C-c c") #'compile))

(add-hook 'java-ts-mode-hook #'nij/java-ts-mode-setup)
#+end_src
*** Javascript/Typescript
Major mode for JS/TS, integrate Prettier, and a REPL.
#+begin_src emacs-lisp
  (use-package typescript-mode
    :ensure t
    :mode (("\\.ts\\'" . typescript-mode)
           ("\\.tsx\\'" . typescript-mode))
    :config
    (setq typescript-indent-level 2))

  ;; Only load prettier-js if the 'prettier' executable is found
  (when (executable-find "prettier")
    (use-package prettier-js
      :ensure t
      :hook ((js-mode . prettier-js-mode)
             (typescript-mode . prettier-js-mode)
             (web-mode . prettier-js-mode))
      :config
      (add-hook 'js-mode-hook
                (lambda ()
                  (add-hook 'before-save-hook 'prettier-js-before-save nil 'local)))
      (add-hook 'typescript-mode-hook
                (lambda ()
                  (add-hook 'before-save-hook 'prettier-js-before-save nil 'local)))
      ))
  (when (executable-find "node")
    (use-package nodejs-repl
      :ensure t
      :hook (js-mode . (lambda ()
                         (define-key js-mode-map (kbd "C-c C-j") 'nodejs-repl-run-current-file)))
      :hook (typescript-mode . (lambda ()
                                 (define-key typescript-mode-map (kbd "C-c C-j") 'nodejs-repl-run-current-file)))
      ))  
#+end_src

*** Haskell
#+begin_src emacs-lisp
    (use-package haskell-mode   :ensure nil :defer t)
#+end_src

** Completion (Corfu)
A lightweight, fast, and modern completion UI (replaces =company=).
=kind-icon= integrates with =all-the-icons= to provide icons,
replacing =company-box=.

#+begin_src emacs-lisp
  (use-package corfu
    :ensure t
    :custom
    ;; These mirror our company settings
    (corfu-auto t)
    (corfu-auto-prefix 1)
    (corfu-auto-delay 0.0)
    (corfu-cycle t)               ; Allow cycling
    (corfu-popupinfo-delay 0.5)   ; Show extra info in a popup
    :bind
    ;; Binds TAB to 'corfu-complete'.
    ;; This is smarter than company-complete-selection:
    ;; - It indents if at the start of a line.
    ;; - It completes if there's a completion.
    (:map corfu-map
          ("<tab>" . corfu-complete)
          ("TAB" . corfu-complete))
    :config
    (global-corfu-mode 1)) ;; in config so it's not lazy, to avoid race condition with kind-icon

  (use-package kind-icon
    :ensure t
    :after corfu
    :config
    ;; This function adds the icons to the corfu popup
    (add-to-list 'corfu-format-functions #'kind-icon-corfu-format))
#+end_src
** Project Management (Projectile)
#+begin_src emacs-lisp
  (use-package projectile
    :ensure t
    ;; :defer t is no longer needed, as :bind-keymap and :init handle it
    :diminish projectile-mode
    :custom ((projectile-completion-system 'default))
    :bind-keymap
     ("C-c p" . projectile-command-map)
    :init
    ;; Enable the global minor mode via its autoload
    (projectile-mode 1)
    
    ;; Set variables before the package is loaded
    (when (file-directory-p "~/Projects/Code")
      (setq projectile-project-search-path '("~/Projects/Code")))
    (setq projectile-switch-project-action #'projectile-dired))

  (use-package consult-projectile
    :ensure t
    :after (projectile consult)
    :bind (:map projectile-command-map
                ("f" . consult-projectile-find-file)
                ("s" . consult-projectile-switch-project)))
#+end_src
** Version Control (Magit)
#+begin_src emacs-lisp
  (use-package transient :ensure t)
  (use-package magit
    :ensure t
    :bind (("C-x g" . magit-status)) ;; This implicitly makes it lazy
    :config
    (setq magit-display-buffer-function #'magit-display-buffer-same-window-except-diff-v1)
    (let ((auth-file (expand-file-name "~/.authinfo.gpg")))
      (when (file-exists-p auth-file)
        (use-package forge
          :ensure t
          :after magit 
          :config (setq auth-sources `(,auth-file))))))
#+end_src
** Rainbow Delimiters
#+begin_src emacs-lisp
  (use-package rainbow-delimiters
    :ensure t
    :hook (prog-mode . rainbow-delimiters-mode))
#+end_src
* File Management (dired)
Some slight customisations . Remember to =:ensure nil=. =dired-single= if we get annoyed by the multiple dired buffers. =dired-open= if we want to directly open binary files in eg feh, mpv, etc.
#+begin_src emacs-lisp
  (use-package dired
     
    :ensure nil
    :commands (dired dired-jump)
    :custom ((dired-listing-switches "-alh --group-directories-first")
    	   (wdired-allow-to-change-permissions t))
    :config
    (require 'dired-x))

  ;; Only load all-the-icons-dired if we are in GUI and fonts are installed
  (when (and (display-graphic-p) (member "all-the-icons" (font-family-list)))
    (use-package all-the-icons-dired
      :ensure t
      :hook (dired-mode . all-the-icons-dired-mode)))

  (use-package dired-hide-dotfiles
    :ensure t
    :hook (dired-mode . dired-hide-dotfiles-mode)
    )

#+end_src
* PDF Tools
From [[https://github.com/vedang/pdf-tools][PDF Tools]] - it is awesome, but it needs libpoppler, poppler-glib and epdfinfo on the host system.
#+begin_src emacs-lisp
  (use-package pdf-tools
    :ensure t
    :defer t
    :commands (pdf-tools-install pdf-loader-install) ; Make install commands available
    :mode ("\\.pdf\\'" . pdf-view-mode)
    :bind (:map pdf-view-mode-map
                ("C-=" . pdf-view-enlarge)
                ("C--" . pdf-view-shrink))
    :config
    ;; Check if the compiled server component is available
    (if (require 'pdf-tools-server nil 'noerror)
        ;; If YES, then configure the package
        (progn
          (setq revert-without-query (concat "\(" (regexp-quote ".pdf") "\|.*\.pdf\.lock\)"))
          ;; Add the hook for your midnight mode here
          (add-hook 'pdf-view-mode-hook 'nij/pdf-view-midnight-mode-setup))
      
      ;; If NO, print a helpful message
      (message "pdf-tools server not compiled. Run 'M-x pdf-tools-install'.")))

  (defun nij/pdf-view-midnight-mode-setup ()
    "Enable pdf-view-midnight-minor-mode in pdf-view-mode."
    (pdf-view-midnight-minor-mode 1))
#+end_src
* LaTeX
Lazy-load AUCTeX when a .tex file is opened.
#+begin_src emacs-lisp
  (use-package auctex
    :ensure t
    :mode ("\\.tex\\'" . LaTeX-mode) ; -- This is the lazy-loading trigger
    :config
    ;; Set the default viewing programs
    (setq TeX-view-program-selection
          '(((output-dvi has-no-display-manager) "dvi2tty")
            ((output-dvi style-pstricks) "dvips and gv")
            (output-dvi "xdvi")
            (output-pdf "PDF Tools")) ; auctex will find pdf-tools
          TeX-view-program-list
          '(("PDF Tools" TeX-pdf-tools-sync-view))
          TeX-source-correlate-start-server t)

    ;; Conditionally add 'xdg-open' for HTML if it exists
    (when (executable-find "xdg-open")
      (add-to-list 'TeX-view-program-selection '(output-html "xdg-open")))

    ;; Refresh buffer after compilation
    (add-hook 'TeX-after-compilation-finished-functions
              #'TeX-revert-document-buffer))
#+end_src
* Vterm
A linux-only terminal, based on the libvterm library. We define a helper function to multiplex it and lazy-load the package via keybindings.
#+begin_src emacs-lisp
  (defun nij/vterm-new ()
    "Open a new vterm buffer with a unique name."
    (interactive)
    (vterm (generate-new-buffer-name "*vterm*")))
    
  (use-package vterm
    :ensure t
    :config
    (setq shell-file-name "/bin/bash"
          vterm-max-scrollback 5000) 
    :bind (("C-c t" . vterm)
           ("C-c T" . nij/vterm-new))) 
#+end_src
* Other things
-  At some point, I should try and get music, video, IRC, RSS, email, and matrix working. 
-  I'd also like to experiment with EXWM
- is it possible to offer to install external dependencies??  
- straight.el or elpaca? 
